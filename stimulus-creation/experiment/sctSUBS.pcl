include "sctINFO.pcl";

## INPUT
sub string GetInput(string prompt) begin
	t_Text.set_caption(prompt, true);
	t_Text2.set_caption("", true);
	p_Text.present();
	system_keyboard.set_case_mode(3);
	string in = system_keyboard.get_input(p_Text, t_Text2);
	return in;
end;

sub ClearText begin
	t_Text.set_caption("", true);
	t_Text2.set_caption("", true);
end;

sub GetParticipantID begin
	s_Participant = GetInput(s_ParticipantID);
	ClearText();
end;

sub WaitForKey begin
	loop i_ButtonPresses = response_manager.total_response_count() until response_manager.total_response_count() > i_ButtonPresses
   begin
	end;
end;

sub InstructParticipant(string message) begin
	t_Text2.set_caption(message, true);
	p_Text.present();
	WaitForKey();
	ClearText();
end;

## STIMULUS LOADING
sub LoadTestFile(string file) begin
	input_file f = new input_file;
	f.open(file);
	f.set_delimiter('\n');
	
	string header = f.get_line();
	
	loop i_Trials = 1 until f.end_of_file() || !f.last_succeeded() begin
		string l = f.get_line();
		l.split("\t", as_Stimuli[i_Trials]);
		i_Trials = i_Trials + 1;
	end;
	
	f.close();
end;

sub ShuffleTrials begin
	loop i_Shuffle = 1 until i_Shuffle > iRep begin
		as_Stimuli.shuffle();
		as_Trials[i_Shuffle] = as_Stimuli;
		i_Shuffle = i_Shuffle + 1;
	end;
end;

sub LoadPracticeFile(string file) begin
	input_file f = new input_file;
	f.open(file);
	f.set_delimiter('\n');
	
	string header = f.get_line();
	
	loop i_Trials = 1 until f.end_of_file() || !f.last_succeeded() begin
		string l = f.get_line();
		l.split("\t", as_Practice[i_Trials]);
		i_Trials = i_Trials + 1;
	end;
end;

## DATA
sub InitialiseStorage begin
	s_fData = "Timings_PP_" + s_Participant + ".txt";
	
	output_file f = new output_file;
	f.open(s_fData, false);
	f.print(s_fHeader);
	f.close();
end;

sub Synchronise begin
	i_BaseTimer = clock.time();
end;

sub Store(int i_cRep, int i_cRow) begin
	string s_subj = s_Participant;
	string s_item = as_Trials[i_cRep][i_cRow][1];
	string s_vari = string(i_cRep);
	string s_onse = string(ai_Timings[i_Shuffle][i_Trials][1]);
	string s_offs = string(ai_Timings[i_Shuffle][i_Trials][2]);
	
	string out = s_subj + "\t" + 
					 s_item + "\t" + 
					 s_vari + "\t" + 
					 s_onse + "\t" + 
					 s_offs + "\n";
	
	output_file f = new output_file;
	f.open_append(s_fData);
	f.print(out);
	f.close();
end;

## TRIALS
sub RunPractice begin
	loop i_Trials = 1 until i_Trials > iPractice begin
		p_Blank.present();
		wait_interval(iBlank);
		
		p_Fixation.present();
		wait_interval(iFixation);
		
		wait_interval(iDelay);
		
		t_Text.set_caption(as_Practice[i_Trials][4], true);
		t_Text2.set_caption("", true);
		p_Text.present();
		wait_interval(iPresentation);
		
		p_Blank.present();
		
		i_Trials = i_Trials + 1;
	end;
end;

sub RunTrials begin
	loop i_Shuffle = 1 until i_Shuffle > iRep begin
		
		if i_Shuffle > 1 then
			ClearText();
			InstructParticipant(s_BlockBreak);
		end;
		
		loop i_Trials = 1 until i_Trials > iRows begin
			
			p_Blank.present();
			wait_interval(iBlank);
			
			p_Fixation.present();
			wait_interval(iFixation);
			
			## we no longer take recordings through presentation; all we need are timings
			#w_Recording.set_base_filename("S-" + s_Participant + "-T-" + as_Trials[i_Shuffle][i_Trials][1] + "-V-" + string(i_Shuffle));
			#w_Recording.present();
			wait_interval(iDelay);
			
			t_Text.set_caption(as_Trials[i_Shuffle][i_Trials][4], true);
			t_Text2.set_caption("", true);
			p_Text.present();
			int onset = clock.time() - i_BaseTimer;
			ai_Timings[i_Shuffle][i_Trials][1] = onset;
			
			wait_interval(iPresentation);
			
			p_Blank.present();
			
			int offset = clock.time() - i_BaseTimer;
			ai_Timings[i_Shuffle][i_Trials][2] = offset;
			
			Store(i_Shuffle, i_Trials);
			i_Trials = i_Trials + 1;
		end;
		
		i_Shuffle = i_Shuffle + 1;
	end;
end;